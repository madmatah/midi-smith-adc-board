cmake_minimum_required(VERSION 3.22)

#
# This file is generated only once,
# and is not re-generated if converter is called multiple times.
#
# User is free to modify the file as much as necessary
#

# Setup compiler settings
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS ON)


# Define the build type
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Debug")
endif()

# Set the project name
set(CMAKE_PROJECT_NAME adc-board)

# Enable compile command to ease indexing with e.g. clangd
set(CMAKE_EXPORT_COMPILE_COMMANDS TRUE)

# Core project settings
project(${CMAKE_PROJECT_NAME})
message("Build type: " ${CMAKE_BUILD_TYPE})

# Enable CMake support for ASM and C languages
enable_language(C ASM CXX)

# Set the C++ standard
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS ON)

if(HOST_TESTS)
    enable_testing()

    add_library(domain STATIC)
    target_sources(domain PRIVATE
        domain/src/shell/command_parser.cpp
        domain/src/shell/line_editor.cpp
        domain/src/shell/command_dispatcher.cpp
        domain/src/shell/shell_engine.cpp
        domain/src/shell/commands/version_command.cpp
    )
    target_include_directories(domain PUBLIC
        domain/include
    )

    add_subdirectory(tests)
else()

# Create an executable object type
add_executable(${CMAKE_PROJECT_NAME})

# Versioning
include(cmake/version/versioning.cmake)

# Add compile options for embedded C++
target_compile_options(${CMAKE_PROJECT_NAME} PRIVATE
    $<$<COMPILE_LANGUAGE:CXX>:-fno-exceptions -fno-rtti>
)

# Add STM32CubeMX generated sources
add_subdirectory(cmake/stm32cubemx)

# Link directories setup
target_link_directories(${CMAKE_PROJECT_NAME} PRIVATE
    # Add user defined library search paths
)

# Add sources to executable
target_sources(${CMAKE_PROJECT_NAME} PRIVATE
    # Add user sources here
    app/src/application.cpp
    app/src/app_entry.cpp
    app/src/version.cpp
    app/src/composition/analog_subsystem.cpp
    app/src/composition/shell_subsystem.cpp
    app/src/composition/sensor_rtt_telemetry_subsystem.cpp
    app/src/tasks/shell_task.cpp
    app/src/tasks/analog_acquisition_task.cpp
    app/src/tasks/sensor_rtt_telemetry_task.cpp
    domain/src/shell/command_parser.cpp
    domain/src/shell/line_editor.cpp
    domain/src/shell/command_dispatcher.cpp
    domain/src/shell/shell_engine.cpp
    domain/src/shell/commands/version_command.cpp
    bsp/src/board.cpp
    bsp/src/cortex/axi_sram_nocache_mpu.cpp
    bsp/src/cortex/d3_sram_nocache_mpu.cpp
    bsp/src/adc/adc_dma.cpp
    bsp/src/adc/adc_dma_callbacks.cpp
    bsp/src/time/tim2_timestamp_counter.cpp
    bsp/src/gpio.cpp
    bsp/src/pins.cpp
    bsp/src/rtt_logger.cpp
    bsp/src/rtt_telemetry_sender.cpp
    bsp/src/serial/uart_stream_registry.cpp
    app/src/shell/commands/adc_command.cpp
    app/src/shell/commands/sensor_rtt_command.cpp
    Third_Party/SEGGER/RTT/RTT/SEGGER_RTT.c
    Third_Party/SEGGER/RTT/RTT/SEGGER_RTT_printf.c
    os/src/clock.cpp
    os/src/task.cpp
    os/src/queue.cpp
    os/src/binary_semaphore.cpp
    os/src/memory.cpp
    os/src/this_task.cpp
)


# Add include paths
target_include_directories(${CMAKE_PROJECT_NAME} PRIVATE
    # Add user defined include paths
    app/include
    ${CMAKE_BINARY_DIR}/generated
    bsp/include
    Third_Party/SEGGER/RTT/RTT
    Third_Party/SEGGER/RTT/Config
    os/include
    domain/include
)

target_compile_definitions(${CMAKE_PROJECT_NAME} PRIVATE
    SEGGER_RTT_SECTION=\".rtt\"
)

# Add project symbols (macros)
target_compile_definitions(${CMAKE_PROJECT_NAME} PRIVATE
    # Add user defined symbols
)

# Remove wrong libob.a library dependency when using cpp files
list(REMOVE_ITEM CMAKE_C_IMPLICIT_LINK_LIBRARIES ob)

# Add linked libraries
target_link_libraries(${CMAKE_PROJECT_NAME}
    stm32cubemx

    # Add user defined libraries
)

add_dependencies(${CMAKE_PROJECT_NAME} version_check)

endif()

# Add a task to format the source code
file(GLOB_RECURSE ALL_SRC "*.cpp" "*.hpp" "*.c" "*.h")

set(FILES_TO_FORMAT "")
foreach(FILE_PATH ${ALL_SRC})
    # Exclude autogenerated code from formatting
    if(NOT FILE_PATH MATCHES "(Core/|Drivers/|Middlewares/|Third_Party/|USB_DEVICE/|build/|cmake/|\\.cache/)")
        list(APPEND FILES_TO_FORMAT ${FILE_PATH})
    endif()
endforeach()

add_custom_target(format
    COMMAND clang-format --verbose -i ${FILES_TO_FORMAT}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Format source code"
)

#
# Lint (project-owned code only)
#
file(GLOB_RECURSE LINT_SRC CONFIGURE_DEPENDS
    "app/*.*"
    "bsp/*.*"
    "os/*.*"
    "domain/*.*"
    "tests/*.*"
)

set(FILES_TO_LINT "")
foreach(FILE_PATH ${LINT_SRC})
    if(FILE_PATH MATCHES "\\.(cpp|hpp|h)$")
        list(APPEND FILES_TO_LINT ${FILE_PATH})
    endif()
endforeach()

add_custom_target(lint
    COMMAND cpplint --quiet ${FILES_TO_LINT}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "cpplint (project-owned code only)"
)


# Update build/current symlink after each preset change
if(CMAKE_EXPORT_COMPILE_COMMANDS)
    file(MAKE_DIRECTORY "${CMAKE_SOURCE_DIR}/build")
    message(STATUS "Updating build/current symlink to : ${CMAKE_BINARY_DIR}")
    execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink
        "${CMAKE_BINARY_DIR}"
        "${CMAKE_SOURCE_DIR}/build/current")
endif()

